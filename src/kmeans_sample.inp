clear
set verbose off
set seed 1234

include kmeans.gfn

# Select an example to run
EXAMPLE = 2

if EXAMPLE == 1
    open mccullagh.gdt --quiet

    list x = Months Acc Type
    n_clusters = 2

elif EXAMPLE == 2
    open credscore.gdt --quiet
    list x = Age Income Avgexp
    scalar n_clusters = 3

elif EXAMPLE == 3
    # NOTE: You need to install the "PairPlot" package first
    open iris.gdt --frompkg=PairPlot --quiet
    list x = 1..4
    scalar n_clusters = 3
endif

smpl x --no-missing

# Euclidean distance (default)
# ============================
bundle Model = kmeans_fit(x, n_clusters)
print Model
eval Model.centroids

kmeans_summary(Model)  # Print summary of estimation

series fc_cluster = kmeans_predict(x, Model)
series cluster_id = Model.cluster_id  # must equal "fc_cluster"
series distances = Model.distances
print distances cluster_id fc_cluster -o

# Plot distinct clusters for each variable combination in 2D
kmeans_plot(x, Model)

/* You can also manipulate the plot if wanted:
Model.use_circles = TRUE
Model.pointsize = 0.25
Model.filename = "foo2.png"     # pass filename for storing plot
kmeans_plot(x, Model)
*/


# Manhattan distance
# ==================
# Pass some user-defined parameter values, too
bundle opt = _(max_iter = 100, n_draws = 20, distance_type = "manhattan")
bundle Model = kmeans_fit(x, 4, opt)

# Plot distinct clusters
kmeans_plot(x, Model)

